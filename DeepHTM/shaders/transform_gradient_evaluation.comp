#version 430

#ifndef EXTERNAL_PARAMETERS
	#define OUTPUT_COUNT 1
#endif

layout(binding = 0) buffer InputGradients
{
	float inputGradients[];
};

layout(binding = 1) buffer OutputGradients
{
	float outputGradients[];
};

layout(binding = 2) buffer Weights
{
	float weights[];
};

layout(local_size_x = OUTPUT_COUNT) in;

shared float localSums[OUTPUT_COUNT];

void main()
{
	const uint inputCount = gl_NumWorkGroups.x;
	const uint localInput = gl_WorkGroupID.x, localOutput = gl_LocalInvocationID.x, minibatch = gl_WorkGroupID.y;
	const uint globalInput = localInput + minibatch * inputCount, globalOutput = localOutput + minibatch * OUTPUT_COUNT;

	localSums[localOutput] = outputGradients[globalOutput] * weights[localInput + localOutput * inputCount];

	for(uint stride = OUTPUT_COUNT >> 1u; stride > 0u; stride >>= 1u)
	{
		barrier();

		if(localOutput < stride)
		{
			localSums[localOutput] += localSums[localOutput + stride];
		}
	}

	if(localOutput == 0u)
	{
		inputGradients[globalInput] = localSums[0u];
	}
}